## Upstream: https://github.com/simulationcraft/simc/blob/thewarwithin/ActionPriorityLists/default/mage_arcane.simc
## SimulationCraft Commit Sync: dfeacf0
## Date: 2025-08-28

actions.precombat+=/arcane_intellect
actions.precombat+=/mirror_image
## [Hekili] Create spec option?
# Option to sim offsetting your primary burst into the Arcane Soul window as Sunfury.
actions.precombat+=/variable,name=soul_burst,op=set,value=0
actions.precombat+=/variable,name=soul_cd,op=set,value=1,value_else=0,if=set_bonus.thewarwithin_season_3_4pc>0&talent.spellfire_spheres&talent.resonance&!talent.magis_spark&(active_enemies>=3)&variable.soul_burst
actions.precombat+=/evocation,if=talent.evocation&!variable.soul_cd
actions.precombat+=/arcane_surge,if=variable.soul_cd

actions+=/counterspell
# Spellsteal, but not during CDs.
actions+=/spellsteal,if=buff.arcane_surge.down&debuff.touch_of_the_magi.down&buff.arcane_soul.down&buff.siphon_storm.down
# Tempered Potions last 30s so we generally will use them at the start of our cooldown windows.
actions+=/potion,if=(buff.siphon_storm.up|(!talent.evocation&cooldown.arcane_surge.ready)|((cooldown.arcane_surge.ready|buff.arcane_surge.up)&variable.soul_cd))|boss&fight_remains<30
# Lights Judgment is the only racial ability worth using and is only worthwhile to use outside of cooldowns.
actions+=/lights_judgment,if=(buff.arcane_surge.down&debuff.touch_of_the_magi.down&buff.arcane_soul.down&buff.siphon_storm.down&active_enemies>=2)
# Racials are used after Surge.
actions+=/berserking,if=(buff.siphon_storm.up&variable.soul_cd)|(prev_gcd.1.arcane_surge&!variable.soul_cd)
actions+=/blood_fury,if=(buff.siphon_storm.up&variable.soul_cd)|(prev_gcd.1.arcane_surge&!variable.soul_cd)
actions+=/fireblood,if=(buff.siphon_storm.up&variable.soul_cd)|(prev_gcd.1.arcane_surge&!variable.soul_cd)
actions+=/ancestral_call,if=(buff.siphon_storm.up&variable.soul_cd)|(prev_gcd.1.arcane_surge&!variable.soul_cd)
# Invoke Externals with cooldowns except Autumn which should come just after cooldowns
## actions+=/invoke_external_bufname=power_infusion,if=prev_gcd.1.arcane_surge
## actions+=/invoke_external_buff,name=blessing_of_autumn,if=cooldown.touch_of_the_magi.remains>5
# Trinket specific use cases vary, default is just after Surge.
actions+=/variable,name=steroid_trinket_equipped,op=set,value=equipped.gladiators_badge|equipped.signet_of_the_priory|equipped.high_speakers_accretion|equipped.spymasters_web|equipped.treacherous_transmitter|equipped.imperfect_ascendancy_serum|equipped.quickwick_candlestick|equipped.soulletting_ruby|equipped.funhouse_lens|equipped.house_of_cards|equipped.flarendos_pilot_light|equipped.signet_of_the_priory|equipped.neural_synapse_enhancer
actions+=/variable,name=nonsteroid_trinket_equipped,op=set,value=equipped.blastmaster3000|equipped.ratfang_toxin|equipped.ingenious_mana_battery|equipped.geargrinders_spare_keys|equipped.ringing_ritual_mud|equipped.goo_blin_grenade|equipped.noggenfogger_ultimate_deluxe|equipped.garbagemancers_last_resort|equipped.mad_queens_mandate|equipped.fearbreakers_echo|equipped.mereldars_toll|equipped.gooblin_grenade
actions+=/use_items,if=(((!variable.soul_cd&prev_gcd.1.arcane_surge)|(variable.soul_cd&buff.siphon_storm.up&debuff.touch_of_the_magi.up))&(variable.steroid_trinket_equipped|(!variable.steroid_trinket_equipped&!variable.nonsteroid_trinket_equipped)))|(!variable.steroid_trinket_equipped&variable.nonsteroid_trinket_equipped)|(variable.nonsteroid_trinket_equipped&buff.siphon_storm.remains<10&(cooldown.evocation.remains>17|trinket.cooldown.remains>20))|boss&fight_remains<20
## [Hekili] Moved this up, I don't like creating variables unless I'm actually going to use them.
actions+=/arcane_barrage,line_cd=5,if=fight_remains<2
## [Hekili] Moved these down, I don't like creating variables until I'm actually going to use them.
actions+=/variable,name=aoe_target_count,op=reset,default=2
actions+=/variable,name=aoe_target_count,op=set,value=9,if=!talent.arcing_cleave
actions+=/variable,name=aoe_list,default=0,op=reset
actions+=/variable,name=opener,op=set,value=debuff.touch_of_the_magi.up|action.touch_of_the_magi.time_since<time|cooldown.touch_of_the_magi.remains>=2*gcd.max
## [Hekili] The cd_opener lists is for opening CDs, but not specifically start of combat.  Sims consistently exit cd_opener with TOTM on CD.
actions+=/call_action_list,name=cd_opener,strict_if=variable.soul_cd=0,if=!variable.soul_cd&(cooldown.touch_of_the_magi.remains<2*gcd.max|cooldown.evocation.remains<2*gcd.max|cooldown.arcane_surge.remains<2*gcd.max)
actions+=/call_action_list,name=cd_opener_soul,strict_if=variable.soul_cd=1,if=variable.soul_cd&(cooldown.touch_of_the_magi.remains<2*gcd.max|cooldown.evocation.remains<2*gcd.max|cooldown.arcane_surge.remains<2*gcd.max)
actions+=/call_action_list,name=sunfury,strict=1,if=talent.spellfire_spheres
actions+=/call_action_list,name=spellslinger,strict=1,if=!talent.spellfire_spheres
actions+=/arcane_barrage

# Touch of the Magi used if you just used Arcane Surge, the wait simulates the time it takes to queue another spell after Touch when you Surge into Touch, otherwise we'll Touch off cooldown either after Barrage or if we just need Charges.
actions.cd_opener=touch_of_the_magi,use_off_gcd=1,if=prev_gcd.1.arcane_surge|(cooldown.arcane_surge.remains>30&cooldown.touch_of_the_magi.ready&((buff.arcane_charge.stack<4&!prev_gcd.1.arcane_barrage)|prev_gcd.1.arcane_barrage))|boss&fight_remains<15
actions.cd_opener+=/arcane_blast,if=buff.presence_of_mind.up
# Use Orb for Charges on the opener if you have High Voltage as the Missiles will generate the remaining Charge you need
actions.cd_opener+=/arcane_orb,if=talent.high_voltage&variable.opener,line_cd=10
# Barrage before Evocation if Tempo will expire
actions.cd_opener+=/arcane_barrage,if=buff.arcane_tempo.up&cooldown.evocation.ready&buff.arcane_tempo.remains<gcd.max*5,line_cd=11
actions.cd_opener+=/evocation,if=cooldown.arcane_surge.remains<(gcd.max*3)&cooldown.touch_of_the_magi.remains<(gcd.max*5)|fight_remains<25
# Use Missiles to get Nether Precision up for your burst window, clipping logic applies as long as you don't have Aether Attunement.
actions.cd_opener+=/arcane_missiles,if=(prev_gcd.1.evocation|prev_gcd.1.arcane_surge|variable.opener)&buff.nether_precision.down,interrupt_if=tick_time>gcd.remains&buff.aether_attunement.down,interrupt_immediate=1,interrupt_global=1,chain=1,line_cd=30
# [Hekili] TODO: How much Arcane Surge CD recovery should we waste if TotM isn't ready?
actions.cd_opener+=/arcane_surge,if=cooldown.touch_of_the_magi.remains<(action.arcane_surge.execute_time+(gcd.max*(buff.arcane_charge.stack=4)))|boss&fight_remains<25

# Alternate tier set opener for AOE. Surge, then Evocate 5-7s later, then Touch when Surge has a few seconds remaining.
actions.cd_opener_soul=arcane_surge,if=(cooldown.touch_of_the_magi.remains<15)
actions.cd_opener_soul+=/evocation,if=buff.arcane_surge.up&(buff.arcane_surge.remains<=8.5|((buff.glorious_incandescence.up|buff.intuition.up)&buff.arcane_surge.remains<=10))
actions.cd_opener_soul+=/touch_of_the_magi,if=(buff.arcane_surge.remains<=2.5&prev_gcd.1.arcane_barrage)|(cooldown.evocation.remains>40&cooldown.evocation.remains<60&prev_gcd.1.arcane_barrage)

# With Shifting Shards we can use Shifting Power whenever basically favoring cooldowns slightly, without it though we want to use it outside of cooldowns, don't cast if it'll conflict with Intuition expiration.
actions.spellslinger+=/shifting_power,if=(((((action.arcane_orb.charges=0)&cooldown.arcane_orb.remains>16)|cooldown.touch_of_the_magi.remains<20)&buff.arcane_surge.down&buff.siphon_storm.down&debuff.touch_of_the_magi.down&(buff.intuition.down|(buff.intuition.up&buff.intuition.remains>cast_time))&cooldown.touch_of_the_magi.remains>(12+6*gcd.max))|(prev_gcd.1.arcane_barrage&talent.shifting_shards&(buff.intuition.down|(buff.intuition.up&buff.intuition.remains>cast_time))&(buff.arcane_surge.up|debuff.touch_of_the_magi.up|cooldown.evocation.remains<20)))&fight_remains>10&(buff.arcane_tempo.remains>gcd.max*2.5|buff.arcane_tempo.down)
# In single target, use Presence of Mind at the very end of Touch of the Magi, then cancelaura the buff to start the cooldown, wait is to simulate the delay of hitting Presence of Mind after another spell cast.
actions.spellslinger+=/cancel_buff,name=presence_of_mind,use_off_gcd=1,if=prev_gcd.1.arcane_blast&buff.presence_of_mind.stack=1
actions.spellslinger+=/presence_of_mind,if=debuff.touch_of_the_magi.remains<=gcd.max&buff.nether_precision.up&active_enemies<variable.aoe_target_count&!talent.unerring_proficiency
## actions.spellslinger+=/wait,sec=0.05,if=aaction.presence_of_mind.time_since<0.015
actions.spellslinger+=/supernova,if=debuff.touch_of_the_magi.remains<=gcd.max&buff.unerring_proficiency.stack=30
# Orb if you need charges.
actions.spellslinger+=/arcane_orb,if=buff.arcane_charge.stack<4
# Barrage if Tempo is about to expire.
actions.spellslinger+=/arcane_barrage,if=(buff.arcane_tempo.up&buff.arcane_tempo.remains<gcd.max)
# Use Aether Attunement up before casting Touch if you have S2 4pc equipped to avoid munching.
actions.spellslinger+=/arcane_missiles,if=buff.aether_attunement.up&cooldown.touch_of_the_magi.remains<gcd.max*3&buff.clearcasting.up&set_bonus.tww2>3
# Barrage if Touch is up or will be up while Barrage is in the air.
actions.spellslinger+=/arcane_barrage,if=(cooldown.touch_of_the_magi.ready|cooldown.touch_of_the_magi.remains<((travel_time+0.05)>?gcd.max))&(cooldown.arcane_surge.remains>30&cooldown.arcane_surge.remains<75)
# Anticipate the Intuition granted from the Season 3 set bonus.
actions.spellslinger+=/arcane_barrage,if=buff.arcane_charge.stack=4&buff.arcane_harmony.stack>=20&set_bonus.thewarwithin_season_3_4pc
# Use Clearcasting procs to keep Nether Precision up, if you don't have S2 4pc try to pool Aether Attunement for cooldown windows.
actions.spellslinger+=/arcane_missiles,if=(buff.clearcasting.up&buff.nether_precision.down&((cooldown.touch_of_the_magi.remains>gcd.max*7&cooldown.arcane_surge.remains>gcd.max*7)|buff.clearcasting.stack>1|!talent.magis_spark|(cooldown.touch_of_the_magi.remains<gcd.max*4&buff.aether_attunement.down)|set_bonus.thewarwithin_season_2_4pc))|(fight_remains<5&buff.clearcasting.up),interrupt_if=tick_time>gcd.remains&(buff.aether_attunement.down|(active_enemies>3&(!talent.time_loop|talent.resonance))),interrupt_immediate=1,interrupt_global=1,chain=1
# Missile to refill charges if you have High Voltage and either Aether Attunement or more than one Clearcasting proc. Recheck AOE
actions.spellslinger+=/arcane_missiles,if=talent.high_voltage&(buff.clearcasting.stack>1|(buff.clearcasting.up&buff.aether_attunement.up))&buff.arcane_charge.stack<3,interrupt_if=tick_time>gcd.remains&(buff.aether_attunement.down|(active_enemies>3&(!talent.time_loop|talent.resonance))),interrupt_immediate=1,interrupt_global=1,chain=1
# Use Intuition.
actions.spellslinger+=/arcane_barrage,if=buff.intuition.up
# Make sure to always activate Spark!
actions.spellslinger+=/arcane_blast,if=debuff.magis_spark_arcane_blast.up|buff.leydrinker.up,line_cd=2
# In single target, spending your Nether Precision stacks on Blast is a higher priority in single target.
actions.spellslinger+=/arcane_blast,if=buff.nether_precision.up&buff.arcane_harmony.stack<=16&buff.arcane_charge.stack=4&active_enemies=1
# Barrage if you're going to run out of mana and have Orb ready.
actions.spellslinger+=/arcane_barrage,if=mana.pct<10&buff.arcane_surge.down&(cooldown.arcane_orb.remains<gcd.max)
# Orb in ST if you don't have Charged Orb, will overcap soon, and before entering cooldowns.
actions.spellslinger+=/arcane_orb,if=active_enemies=1&(cooldown.touch_of_the_magi.remains<6|!talent.charged_orb|buff.arcane_surge.up|cooldown.arcane_orb.charges_fractional>1.5)
# Barrage if you have orb coming off cooldown in AOE and you don't have enough harmony stacks to make it worthwhile to hold for set proc.
actions.spellslinger+=/arcane_barrage,if=active_enemies>=2&buff.arcane_charge.stack=4&cooldown.arcane_orb.remains<gcd.max&(buff.arcane_harmony.stack<=(8+(10*!set_bonus.thewarwithin_season_3_4pc)))&(((prev_gcd.1.arcane_barrage|prev_gcd.1.arcane_orb)&buff.nether_precision.stack=1)|buff.nether_precision.stack=2|buff.nether_precision.down)
actions.spellslinger+=/arcane_barrage,if=active_enemies>2&(buff.arcane_charge.stack=4&!set_bonus.thewarwithin_season_3_4pc)
# Orb if you're low on Harmony stacs.
actions.spellslinger+=/arcane_orb,if=active_enemies>1&buff.arcane_harmony.stack<20&(buff.arcane_surge.up|buff.nether_precision.up|active_enemies>=7)&set_bonus.thewarwithin_season_3_4pc
# Arcane Barrage in AOE if you have Aether Attunement ready and High Voltage
actions.spellslinger+=/arcane_barrage,if=talent.high_voltage&active_enemies>=2&buff.arcane_charge.stack=4&buff.aether_attunement.up&buff.clearcasting.up
# Use Orb more aggressively if cleave and a little less in AOE.
actions.spellslinger+=/arcane_orb,if=active_enemies>1&(active_enemies<3|buff.arcane_surge.up|(buff.nether_precision.up))&set_bonus.thewarwithin_season_3_4pc
# Barrage if Orb is available in AOE.
actions.spellslinger+=/arcane_barrage,if=active_enemies>1&buff.arcane_charge.stack=4&cooldown.arcane_orb.remains<gcd.max
# If you have High Voltage throw out a Barrage before you need to use Clearcasting for NP.
actions.spellslinger+=/arcane_barrage,if=talent.high_voltage&buff.arcane_charge.stack=4&buff.clearcasting.up&buff.nether_precision.stack=1
# Barrage with Orb Barrage or execute if you have orb up and no Nether Precision or no way to get another and use Arcane Orb to recover Arcane Charges, hold resources for Touch of the Magi if you have Magi's Spark. Skip this with Season 3 set.
actions.spellslinger+=/arcane_barrage,if=(active_enemies=1&(talent.orb_barrage|(target.health.pct<35&talent.arcane_bombardment))&(cooldown.arcane_orb.remains<gcd.max)&buff.arcane_charge.stack=4&(cooldown.touch_of_the_magi.remains>gcd.max*6|!talent.magis_spark)&(buff.nether_precision.down|(buff.nether_precision.stack=1&buff.clearcasting.stack=0)))&!set_bonus.thewarwithin_season_3_4pc
# Use Explosion for your first charge or if you have High Voltage you can use it for charge 2 and 3, but at a slightly higher target count.
actions.spellslinger+=/arcane_explosion,if=active_enemies>1&((buff.arcane_charge.stack<1&!talent.high_voltage)|(buff.arcane_charge.stack<3&(buff.clearcasting.down|talent.reverberate)))
# You can use Arcane Explosion in single target for your first 2 charges when you have no Clearcasting procs and aren't out of mana. This is only a very slight gain for some profiles so don't feel you have to do this.
actions.spellslinger+=/arcane_explosion,if=active_enemies=1&buff.arcane_charge.stack<2&buff.clearcasting.down
# Barrage in execute if you're at the end of Touch or at the end of Surge windows. Skip this with Season 3 set.
actions.spellslinger+=/arcane_barrage,if=(((target.health.pct<35&(debuff.touch_of_the_magi.remains<(gcd.max*1.25))&(debuff.touch_of_the_magi.remains>action.arcane_barrage.travel_time))|((buff.arcane_surge.remains<gcd.max)&buff.arcane_surge.up))&buff.arcane_charge.stack=4)&!set_bonus.thewarwithin_season_3_4pc
# Nothing else to do? Blast. Out of mana? Barrage.
actions.spellslinger+=/arcane_blast
actions.spellslinger+=/arcane_barrage

# For Sunfury, Shifting Power only when you're not under the effect of any cooldowns.
actions.sunfury=shifting_power,if=((buff.arcane_surge.down&buff.siphon_storm.down&debuff.touch_of_the_magi.down&cooldown.evocation.remains>15&cooldown.touch_of_the_magi.remains>10)&fight_remains>10)&buff.arcane_soul.down&(buff.intuition.down|(buff.intuition.up&buff.intuition.remains>cast_time))
actions.sunfury+=/cancel_buff,name=presence_of_mind,use_off_gcd=1,if=(prev_gcd.1.arcane_blast&buff.presence_of_mind.stack=1)|active_enemies<4
actions.sunfury+=/presence_of_mind,if=debuff.touch_of_the_magi.remains<=gcd.max&buff.nether_precision.up&active_enemies<4
## actions.sunfury+=/wait,sec=0.05,if=action.presence_of_mind.time_since<0.015,line_cd=15
# When Arcane Soul is up, use Missiles to generate Nether Precision as needed while also ensuring you end Soul with 3 Clearcasting.
actions.sunfury+=/arcane_missiles,if=buff.nether_precision.down&buff.clearcasting.up&buff.arcane_soul.up&buff.arcane_soul.remains>gcd.max*(4-buff.clearcasting.stack),interrupt_if=tick_time>gcd.remains,interrupt_immediate=1,interrupt_global=1,chain=1
actions.sunfury+=/arcane_barrage,if=buff.arcane_soul.up
# Dump a clearcasting proc before you go into Soul if you have one.
actions.sunfury+=/arcane_missiles,if=buff.clearcasting.up&buff.arcane_surge.up&buff.arcane_surge.remains<gcd.max,interrupt_if=tick_time>gcd.remains,interrupt_immediate=1,interrupt_global=1,chain=1
# Prioritize Tempo and Intuition if they are about to expire.
actions.sunfury+=/arcane_barrage,if=(buff.arcane_tempo.up&buff.arcane_tempo.remains<(gcd.max+(gcd.max*buff.nether_precision.stack=1)))|(buff.intuition.up&buff.intuition.remains<(gcd.max+(gcd.max*buff.nether_precision.stack=1)))
# Gamble on Orb Barrage in AOE to prevent overcapping on Harmony stacks.
actions.sunfury+=/arcane_barrage,if=(talent.orb_barrage&active_enemies>1&buff.arcane_harmony.stack>=18&((active_enemies>3&(talent.resonance|talent.high_voltage))|buff.nether_precision.down|buff.nether_precision.stack=1|(buff.nether_precision.stack=2&buff.clearcasting.stack=3)))
# Spend Aether Attunement if you have 4pc S2 set before Touch.
actions.sunfury+=/arcane_missiles,if=buff.clearcasting.up&set_bonus.thewarwithin_season_2_4pc&buff.aether_attunement.up&cooldown.touch_of_the_magi.remains<gcd.max*(3-(1.5*(active_enemies>3&(!talent.time_loop|talent.resonance)))),interrupt_if=tick_time>gcd.remains&(buff.aether_attunement.down|(active_enemies>3&(!talent.time_loop|talent.resonance))),interrupt_immediate=1,interrupt_global=1,chain=1
# Barrage into Touch if you have charges when it comes up.
actions.sunfury+=/arcane_barrage,if=buff.arcane_charge.stack=4&((cooldown.touch_of_the_magi.ready)|cooldown.touch_of_the_magi.remains<((travel_time+50)>?gcd.max))&!variable.soul_cd
actions.sunfury+=/arcane_barrage,if=(cooldown.touch_of_the_magi.ready|(cooldown.touch_of_the_magi.remains<((travel_time+50)>?gcd.max)))&(buff.arcane_surge.down|(buff.arcane_surge.up&buff.arcane_surge.remains<=2.5))&variable.soul_cd
# Blast if Magi's Spark is up.
actions.sunfury+=/arcane_blast,if=debuff.magis_spark_arcane_blast.up&buff.arcane_charge.stack=4,line_cd=2
# AOE Barrage conditions revolve around sending Barrages various talents. Whenever you have Clearcasting and Nether Precision or if you have Aether Attunement to recharge with High Voltage. Whenever you have Orb Barrage you should gamble basically any chance you get in execute. Lastly, with Arcane Orb available, you can send Barrage as long as you're not going to use Touch soon and don't have a reason to use Blast up.
actions.sunfury+=/arcane_barrage,if=(talent.high_voltage&active_enemies>1&buff.arcane_charge.stack=4&buff.clearcasting.up&buff.nether_precision.stack=1)
actions.sunfury+=/arcane_barrage,if=(talent.high_voltage&active_enemies>1&buff.arcane_charge.stack=4&buff.clearcasting.up&buff.aether_attunement.up&buff.glorious_incandescence.down&buff.intuition.down)
actions.sunfury+=/arcane_barrage,if=(active_enemies>2&talent.orb_barrage&talent.high_voltage&debuff.magis_spark_arcane_blast.down&buff.arcane_charge.stack=4&target.health.pct<35&talent.arcane_bombardment&(buff.nether_precision.up|(buff.nether_precision.down&buff.clearcasting.stack=0)))
actions.sunfury+=/arcane_barrage,if=(active_enemies>2|(active_enemies>1&target.health.pct<35&talent.arcane_bombardment))&cooldown.arcane_orb.remains<gcd.max&buff.arcane_charge.stack=4&cooldown.touch_of_the_magi.remains>gcd.max*6&(debuff.magis_spark_arcane_blast.down|!talent.magis_spark)&buff.nether_precision.up&(talent.high_voltage|((buff.leydrinker.down|(target.health.pct<35&talent.arcane_bombardment&active_enemies>=4&talent.resonance))&buff.nether_precision.stack=2)|(buff.nether_precision.stack=1&buff.clearcasting.down))
# Missiles to recoup Charges with High Voltage or maintain Nether Precision and combine it with other Barrage buffs.
actions.sunfury+=/arcane_missiles,if=buff.clearcasting.up&((talent.high_voltage&buff.arcane_charge.stack<4)|(buff.nether_precision.down&(buff.clearcasting.stack>1|buff.spellfire_spheres.stack=6|buff.burden_of_power.up|buff.glorious_incandescence.up|(buff.intuition.up)))),interrupt_if=tick_time>gcd.remains&(buff.aether_attunement.down|(active_enemies>3&(!talent.time_loop|talent.resonance))),interrupt_immediate=1,interrupt_global=1,chain=1
# Arcane Orb to recover Charges quickly if below 3.
actions.sunfury+=/arcane_orb,if=buff.arcane_charge.stack<3
# Barrage with Incandescence or Intuition.
actions.sunfury+=/arcane_barrage,if=buff.glorious_incandescence.up|buff.intuition.up
# In AOE, Presence of Mind is used to build Charges. Arcane Explosion can be used to build your first Charge.
actions.sunfury+=/presence_of_mind,if=(buff.arcane_charge.stack=3|buff.arcane_charge.stack=2)&active_enemies>=3
actions.sunfury+=/arcane_explosion,if=buff.arcane_charge.stack<2&active_enemies>1
actions.sunfury+=/arcane_blast
actions.sunfury+=/arcane_barrage