## https://github.com/simulationcraft/simc/blob/thewarwithin/ActionPriorityLists/default/deathknight_blood.simc
## SimulationCraft Commit Sync: ddeefc8
## Date: 2025-08-06


actions.precombat+=/deaths_caress
actions.precombat+=/variable,name=bone_shield_refresh_value,value=7

actions+=/mind_freeze
actions+=/use_item,name=tome_of_lights_devotion,if=buff.inner_resilience.up,use_off_gcd=1
actions+=/use_item,name=unyielding_netherprism,if=cooldown.dancing_rune_weapon.remains<1|raid&boss&fight_remains<=20,use_off_gcd=1
actions+=/use_items
actions+=/use_item,name=bestinslots,use_off_gcd=1
actions+=/blood_fury,if=buff.dancing_rune_weapon.up
actions+=/berserking,if=buff.dancing_rune_weapon.up
actions+=/ancestral_call,if=buff.dancing_rune_weapon.up
actions+=/fireblood,if=buff.dancing_rune_weapon.up
actions+=/potion,if=buff.dancing_rune_weapon.up
actions+=/vampiric_blood,if=tanking&incoming_damage_5s>=vb_damage&!(buff.dancing_rune_weapon.up|buff.icebound_fortitude.up)
actions+=/variable,name=rp_deficit_threshold,value=15+(10*talent.relish_in_blood.enabled)+(3*talent.runic_attenuation.enabled)+(spell_targets.heart_strike*talent.heartbreaker.enabled*2)
actions+=/raise_dead,use_off_gcd=1
actions+=/blood_tap,if=(rune<=2&rune.time_to_3>gcd&charges_fractional>=1.8)
actions+=/blood_tap,if=(rune<=1&rune.time_to_3>gcd)
actions+=/deaths_caress,if=buff.bone_shield.remains<gcd.max*2
actions+=/death_strike,if=buff.coagulopathy.up&buff.coagulopathy.remains<=gcd
actions+=/any_dnd,if=!buff.death_and_decay.up
actions+=/blood_boil,if=dot.blood_plague.remains<gcd.max*2
actions+=/dancing_rune_weapon
actions+=/call_action_list,name=deathbringer,strict=1,if=hero_tree.deathbringer
actions+=/call_action_list,name=san_drw,strict=1,if=hero_tree.sanlayn&buff.dancing_rune_weapon.up
actions+=/call_action_list,name=sanlayn,strict=1,if=hero_tree.sanlayn

actions.deathbringer+=/rune_tap,if=rune>4
actions.deathbringer+=/bonestorm,if=buff.bone_shield.stack>=5&buff.death_and_decay.remains
actions.deathbringer+=/death_strike,if=runic_power.deficit<variable.rp_deficit_threshold|(buff.dancing_rune_weapon.up&runic_power.deficit<(variable.rp_deficit_threshold+6))
actions.deathbringer+=/soul_reaper,if=active_enemies<=2&buff.reaper_of_souls.up&target.time_to_die>(dot.soul_reaper.remains+5)
actions.deathbringer+=/soul_reaper,if=active_enemies<=2&target.time_to_pct_35<5&target.time_to_die>(dot.soul_reaper.remains+5)
actions.deathbringer+=/reapers_mark
actions.deathbringer+=/blood_boil,if=buff.dancing_rune_weapon.up&!drw.bp_ticking
actions.deathbringer+=/death_and_decay,if=!buff.death_and_decay.up
actions.deathbringer+=/marrowrend,if=buff.exterminate.up|(buff.bone_shield.stack<5&!dot.bonestorm.ticking)
actions.deathbringer+=/death_strike
actions.deathbringer+=/tombstone,if=buff.bone_shield.stack>=8&buff.death_and_decay.remains&cooldown.dancing_rune_weapon.remains>=25
actions.deathbringer+=/blooddrinker,if=!buff.dancing_rune_weapon.up&active_enemies<=2&buff.coagulopathy.remains>3
actions.deathbringer+=/consumption
actions.deathbringer+=/blood_boil
actions.deathbringer+=/heart_strike,if=buff.coagulopathy.stack<5
actions.deathbringer+=/heart_strike
actions.deathbringer+=/soul_reaper,if=buff.reaper_of_souls.up
actions.deathbringer+=/arcane_torrent,if=runic_power.deficit>20

actions.san_drw+=/heart_strike,if=buff.essence_of_the_blood_queen.remains<1.5&buff.essence_of_the_blood_queen.remains
actions.san_drw+=/bonestorm,if=buff.bone_shield.stack>=5&buff.death_and_decay.remains
actions.san_drw+=/death_strike,if=runic_power.deficit<36
actions.san_drw+=/blood_boil,if=!drw.bp_ticking
actions.san_drw+=/any_dnd,if=(active_enemies<=3&buff.crimson_scourge.remains)|(active_enemies>3&!buff.death_and_decay.remains)
actions.san_drw+=/heart_strike
actions.san_drw+=/death_strike
actions.san_drw+=/consumption
actions.san_drw+=/blood_boil

actions.sanlayn+=/blood_boil,if=(!buff.bone_shield.up|buff.bone_shield.remains<1.5|buff.bone_shield.stack<=1)&active_enemies>=2
actions.sanlayn+=/deaths_caress,if=!buff.bone_shield.up|buff.bone_shield.remains<1.5|buff.bone_shield.stack<=1
actions.sanlayn+=/blood_boil,if=dot.blood_plague.remains<3
actions.sanlayn+=/heart_strike,if=(buff.essence_of_the_blood_queen.remains<1.5&buff.essence_of_the_blood_queen.remains&buff.vampiric_strike.remains)
actions.sanlayn+=/bonestorm,if=buff.bone_shield.stack>=5&buff.death_and_decay.remains
actions.sanlayn+=/death_strike,if=runic_power.deficit<20
actions.sanlayn+=/marrowrend,if=buff.bone_shield.stack<6&!dot.bonestorm.ticking
actions.sanlayn+=/consumption,if=buff.infliction_of_sorrow.up&buff.death_and_decay.up
actions.sanlayn+=/heart_strike,if=(buff.infliction_of_sorrow.up|buff.vampiric_strike.up)&buff.death_and_decay.up
actions.sanlayn+=/soul_reaper,if=active_enemies<=2&target.time_to_pct_35<5&target.time_to_die>(dot.soul_reaper.remains+5)
actions.sanlayn+=/blood_boil,if=buff.bone_shield.stack<6&!dot.bonestorm.ticking&active_enemies>=2
actions.sanlayn+=/deaths_caress,if=buff.bone_shield.stack<6&!dot.bonestorm.ticking
actions.sanlayn+=/tombstone,if=buff.bone_shield.stack>=6&buff.death_and_decay.remains&cooldown.dancing_rune_weapon.remains>=25
actions.sanlayn+=/any_dnd,if=(active_enemies<=3&buff.crimson_scourge.remains)|(active_enemies>3&!buff.death_and_decay.remains)
actions.sanlayn+=/blooddrinker,if=active_enemies<=2&buff.coagulopathy.remains>3
actions.sanlayn+=/heart_strike,if=buff.vampiric_strike.up
actions.sanlayn+=/death_strike
actions.sanlayn+=/heart_strike,if=rune>=2
actions.sanlayn+=/consumption
actions.sanlayn+=/blood_boil
actions.sanlayn+=/heart_strike