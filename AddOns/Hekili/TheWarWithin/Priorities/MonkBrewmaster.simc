## https://github.com/simulationcraft/simc/blob/thewarwithin/ActionPriorityLists/default/monk_brewmaster.simc
## SimulationCraft Commit Sync: 14cbb0c
## Date: 2025-08-12

## TODO - squish the celestial brew entries

actions.precombat+=/potion

actions+=/spear_hand_strike
actions+=/diffuse_magic
actions+=/vivify,if=health.pct<=settings.vivify_percent&buff.vivacious_vivification.up
actions+=/purifying_brew,if=tanking&settings.purify_for_celestial&(time_to_max_charges<gcd.max|buff.purified_chi.up&buff.purified_chi.remains<1.5*gcd.max)|cooldown.celestial_brew.remains<2*gcd.max&charges_fractional>1.5
actions+=/celestial_brew,if=tanking&(buff.celestial_brew.down&buff.celestial_infusion.down)&(!talent.improved_celestial_brew|buff.purified_chi.up)&(!settings.max_damage|buff.blackout_combo.down)
## actions+=/purifying_brew,line_cd=5,if=talent.improved_invoke_niuzao_the_black_ox&settings.purify_for_niuzao&pet.niuzao_the_black_ox.active
actions+=/purifying_brew,if=group&(!settings.max_damage|buff.blackout_combo.down)&settings.purify_stagger_currhp>0&stagger.pct>=settings.purify_stagger_currhp
actions+=/purifying_brew,if=solo&(!settings.max_damage|buff.blackout_combo.down)&settings.purify_stagger_currhp>0&stagger.pct>=settings.purify_stagger_currhp*0.5
actions+=/purifying_brew,if=group&(settings.max_damage&buff.blackout_combo.down)&settings.purify_stagger_maxhp>0&stagger.pct>=settings.purify_stagger_maxhp
actions+=/purifying_brew,if=solo&(!settings.max_damage|buff.blackout_combo.down)&settings.purify_stagger_maxhp>0&stagger.pct>=settings.purify_stagger_maxhp*0.5
actions+=/purifying_brew,if=group&(!settings.max_damage|buff.blackout_combo.down)&settings.purify_stagger_currhp=0&settings.purify_stagger_maxhp=0&stagger.pct>20
actions+=/purifying_brew,if=solo&(!settings.max_damage|buff.blackout_combo.down)&settings.purify_stagger_currhp=0&settings.purify_stagger_maxhp=0&stagger.pct>10
actions+=/dampen_harm,if=health.pct<75&incoming_damage_3s>health.max*(0.2+(0.2*group))&buff.fortifying_brew.down
actions+=/fortifying_brew,if=health.pct<50&incoming_damage_3s>health.max*(0.2+(0.2*group))&(buff.dampen_harm.down)
actions+=/touch_of_death
actions+=/expel_harm,if=buff.gift_of_the_ox.stack>4&health.pct<settings.eh_percent
actions+=/potion
actions+=/blood_fury
actions+=/berserking
actions+=/arcane_torrent
actions+=/lights_judgment
actions+=/fireblood
actions+=/ancestral_call
actions+=/bag_of_tricks
actions+=/use_item,name=tome_of_lights_devotion,if=buff.inner_resilience.up
actions+=/use_item,name=unyielding_netherprism,if=buff.weapons_of_order.up&debuff.weapons_of_order_debuff.stack=4
actions+=/use_item,name=unyielding_netherprism,if=!talent.weapons_of_order.enabled
actions+=/use_item,name=lily_of_the_eternal_weave,if=buff.weapons_of_order.up&debuff.weapons_of_order_debuff.stack=4
actions+=/use_item,name=lily_of_the_eternal_weave,if=!talent.weapons_of_order.enabled
actions+=/use_item,name=signet_of_the_priory,if=buff.weapons_of_order.up&debuff.weapons_of_order_debuff.stack=4
actions+=/use_item,name=signet_of_the_priory,if=!talent.weapons_of_order.enabled
actions+=/use_item,slot=trinket1
actions+=/use_item,slot=trinket2
actions+=/black_ox_brew,if=energy<40&(!talent.aspect_of_harmony.enabled|cooldown.celestial_brew.charges_fractional<1)
actions+=/celestial_brew,if=(!settings.max_damage|buff.blackout_combo.down)&buff.aspect_of_harmony_accumulator.value>0.3*health.max&buff.weapons_of_order.up&!dot.aspect_of_harmony_damage.ticking
actions+=/celestial_brew,if=(!settings.max_damage|buff.blackout_combo.down)&buff.aspect_of_harmony_accumulator.value>0.3*health.max&!talent.weapons_of_order.enabled&!dot.aspect_of_harmony_damage.ticking
actions+=/celestial_brew,if=(!settings.max_damage|buff.blackout_combo.down)&fight_remains<20&fight_remains>14&buff.aspect_of_harmony_accumulator.value>0.2*health.max
actions+=/celestial_brew,if=(!settings.max_damage|buff.blackout_combo.down)&buff.aspect_of_harmony_accumulator.value>0.3*health.max&cooldown.weapons_of_order.remains>20&!dot.aspect_of_harmony_damage.ticking
actions+=/celestial_brew,if=(!settings.max_damage|buff.blackout_combo.down)&(cooldown.celestial_brew.charges_fractional>1.8|(cooldown.celestial_brew.charges_fractional>1.2&cooldown.black_ox_brew.up))
actions+=/blackout_kick
actions+=/chi_burst,if=!talent.aspect_of_harmony.enabled|buff.balanced_stratagem_magic.stack>3
actions+=/weapons_of_order
actions+=/invoke_niuzao,if=!talent.call_to_arms.enabled
actions+=/invoke_niuzao,if=talent.call_to_arms.enabled&buff.call_to_arms_invoke_niuzao.down&buff.weapons_of_order.remains<16
actions+=/rising_sun_kick,if=!talent.fluidity_of_motion.enabled
actions+=/keg_smash,if=buff.weapons_of_order.up&(debuff.weapons_of_order_debuff.remains<1.8|debuff.weapons_of_order_debuff.stack<3-buff.blackout_combo.up|(buff.weapons_of_order.remains<3-buff.blackout_combo.up&buff.weapons_of_order.remains<1+cooldown.rising_sun_kick.remains))
actions+=/tiger_palm,if=buff.blackout_combo.up
actions+=/keg_smash,if=talent.scalding_brew.enabled
actions+=/spinning_crane_kick,if=talent.charred_passions.enabled&talent.scalding_brew.enabled&buff.charred_passions.up&buff.charred_passions.remains<3&dot.breath_of_fire.remains<9&active_enemies>4
actions+=/rising_sun_kick,if=talent.fluidity_of_motion.enabled
actions+=/purifying_brew,if=buff.blackout_combo.down&!(talent.call_to_arms.enabled|talent.invoke_niuzao_the_black_ox.enabled)
actions+=/purifying_brew,if=buff.blackout_combo.down&(talent.call_to_arms.enabled|talent.invoke_niuzao_the_black_ox.enabled)&(buff.invoke_niuzao_the_black_ox.up|buff.call_to_arms_invoke_niuzao.up)
actions+=/purifying_brew,if=buff.blackout_combo.down&(talent.call_to_arms.enabled|talent.invoke_niuzao_the_black_ox.enabled)&cooldown.weapons_of_order.remains>10&cooldown.invoke_niuzao_the_black_ox.remains>10
actions+=/breath_of_fire,if=(buff.charred_passions.down&(!talent.scalding_brew.enabled|active_enemies<5))|!talent.charred_passions.enabled|(dot.breath_of_fire.remains<3&talent.scalding_brew.enabled)
actions+=/exploding_keg,if=!talent.rushing_jade_wind.enabled|buff.rushing_jade_wind.up
actions+=/rushing_jade_wind,if=talent.aspect_of_harmony.enabled&((buff.rushing_jade_wind.remains<2.5&buff.rushing_jade_wind.up)|!buff.rushing_jade_wind.up)
actions+=/keg_smash
actions+=/rushing_jade_wind,if=!talent.aspect_of_harmony.enabled&((buff.rushing_jade_wind.remains<2.5&buff.rushing_jade_wind.up)|!buff.rushing_jade_wind.up)
actions+=/tiger_palm,if=energy>40-cooldown.keg_smash.remains*energy.regen
actions+=/spinning_crane_kick,if=energy>40-cooldown.keg_smash.remains*energy.regen
